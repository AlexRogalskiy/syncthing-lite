apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.2"
    dataBinding.enabled = true
    defaultConfig {
        applicationId "net.syncthing.lite"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 7
        versionName "0.1.5"
        multiDexEnabled true
    }
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile = {
                def path = System.getenv("SYNCTHING_LITE_RELEASE_STORE_FILE")
                return (path) ? file(path) : null
            }()
            storePassword System.getenv("SIGNING_PASSWORD") ?: ""
            keyAlias System.getenv("SYNCTHING_LITE_RELEASE_KEY_ALIAS") ?: ""
            keyPassword System.getenv("SIGNING_PASSWORD") ?: ""
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude 'META-INF/*'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-coroutines:$anko_version"
    kapt "com.android.databinding:compiler:$build_tools_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation ("com.github.Nutomic:syncthing-java:0.1.5") {
        exclude group: 'commons-logging', module:'commons-logging'
        exclude group: 'org.apache.httpcomponents', module:'httpclient'
        exclude group: 'org.slf4j'
        exclude group: 'ch.qos.logback'
    }
    // NOTE: httpclient-android seems to be used via reflection somehow. Removing this dependency
    //       silently breaks the app.
    implementation 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    implementation 'sk.baka.slf4j:slf4j-handroid:1.7.26'
    implementation 'com.google.zxing:android-integration:3.3.0'
    implementation ('uk.co.markormesher:android-fab:2.0.0') {
        exclude group: "org.jetbrains.kotlin"
    }
}
